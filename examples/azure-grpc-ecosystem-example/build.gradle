plugins {
    id 'org.springframework.boot' version '3.5.7'
    id "com.google.protobuf" version "0.9.5"
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter', 'junit-vintage'
    }
}

dependencies {
    implementation project(':on-demand:on-demand-spring-boot-starter-grpc')
    implementation project(':azure:spring-boot-starter-azure-grpc-ecosystem')
    implementation project(':azure:request-response-spring-boot-starter-azure-grpc-ecosystem')
    implementation project(':trace:server:correlation-id-trace-grpc-netty')
    implementation project(':trace:mdc-context-grpc-netty')
    implementation project(':request-response:request-response-spring-boot-autoconfigure-grpc-ecosystem')

    implementation("io.grpc:grpc-api:$grpcVersion")
    implementation("io.grpc:grpc-core:$grpcVersion")
    implementation("io.grpc:grpc-context:$grpcVersion")
    implementation("io.grpc:grpc-stub:$grpcVersion")
    //implementation("io.grpc:grpc-inprocess:$grpcVersion")
    implementation("io.grpc:grpc-services:$grpcVersion")
    implementation("io.grpc:grpc-netty:$grpcVersion")
    implementation("io.grpc:grpc-util:$grpcVersion")
    implementation("org.springframework.boot:spring-boot-starter")
    implementation("javax.annotation:javax.annotation-api:1.3.2")

    // added due to grpc plugin dependency resolution problem
    testImplementation "org.junit.platform:junit-platform-launcher"

    testImplementation project(":azure:spring-boot-starter-azure-grpc-ecosystem-test")
    testImplementation project(':azure:request-response-spring-boot-starter-azure-grpc-ecosystem-test')
    testImplementation("org.springframework.boot:spring-boot-starter-test")

    testImplementation project(":test:test-logback-junit")
    testImplementation ("com.google.truth:truth:${googleTruthVersion}")
    testImplementation ("com.google.truth.extensions:truth-java8-extension:${googleTruthVersion}")

}

sourceSets {
    test.java.srcDirs += "${protobuf.generatedFilesBaseDir}/test/java"
    test.java.srcDirs += "${protobuf.generatedFilesBaseDir}/test/grpc"
}

tasks.compileTestJava { dependsOn("generateTestProto") }

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${grpcProtobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
