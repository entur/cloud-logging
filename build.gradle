
plugins {
    id 'com.github.ben-manes.versions' version '0.51.0' apply false
    id 'org.owasp.dependencycheck' version '11.0.0' apply false
    id "org.sonarqube" version "5.1.0.4882"
    id('io.github.gradle-nexus.publish-plugin') version '1.3.0'
}

// Split projects into buildable: examples and libraries. 
// This omits 'wrapper' directories. See also settings.gradle
 
def buildProjects() {
    subprojects.findAll { new File(it.projectDir, 'build.gradle').file }
}

def platformProjects() {
    buildProjects().findAll { it.name.equals("bom") }
}

def exampleProjects() {
    buildProjects().findAll { it.name.endsWith("-example") }
}

def libraryProjects() {
    buildProjects().findAll { !exampleProjects().contains(it) && !platformProjects().contains(it) }
}

def jvmProjects() {
    buildProjects().findAll { !platformProjects().contains(it) }
}

configure(exampleProjects()) {
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configure(libraryProjects()) {
    apply plugin: 'java-library'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configure(jvmProjects()) {
    apply plugin: 'com.github.ben-manes.versions'

    ext {
        junitJupiterVersion = '5.11.2'
        mockitoVersion = '5.14.1'
        logbackLogstashVersion = '7.4'
        slf4jVersion = '2.0.16'
        googleTruthVersion = '1.4.4'
        logbackVersion = '1.5.9'
        jacksonVersion = '2.18.0'
        jacksonDatabindVersion = '2.18.0'
        nettyTcnativeBoringsslStaticVersion = '2.0.66.Final'
        nettyVersion = '4.1.114.Final'
        micrometerVersion = '1.13.5'
        springBootVersion = '3.3.4' // https://repo1.maven.org/maven2/org/springframework/boot/spring-boot-dependencies/
        janinoVersion = '3.1.12'
        servletVersion = '6.0.0' // jakarta
        logbookVersion = '3.9.0'
        jacksonSyntaxHighlightVersion = '1.1.0'
        logbackLogstashSyntaxHighlightingDecoratorsVersion = '1.1.0'
        commonsIoVersion = '2.17.0'
        commonsTextVersion = '1.12.0'

        grpcNettyVersion = '1.68.0'
        grpcVersion = '1.68.0'
        grpcCommonsVersion = '2.46.0'
        grpcProtobufVersion = '3.25.5'

        jsonAssertVersion = '0.8.0'
        jsonPathVersion = '2.9.0'
        jsonSmartVersion = '2.5.1'

        lognetVersion = '5.1.5'
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter', 'junit-vintage'
        }
    }

    dependencies {
        components.all(LogbackAlignmentRule)
        components.all(GrpcAlignmentRule)
        components.all(ProtobufAlignmentRule)

        // JUnit Jupiter API and TestEngine implementation
        testImplementation("org.junit.jupiter:junit-jupiter:${junitJupiterVersion}")

        testImplementation("org.mockito:mockito-core:${mockitoVersion}")

        testImplementation("com.google.truth:truth:${googleTruthVersion}")
        testImplementation("com.google.truth.extensions:truth-java8-extension:${googleTruthVersion}")
    }

}

// projects which should be published
configure(libraryProjects()) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {

        publications {
            maven(MavenPublication) {
                from components.java

                groupId = "$group"
                artifactId = project.name
                version = "$version"

                pom {
                    name = project.name
                    url = 'https://github.com/entur/cloud-logging'
                    packaging = 'jar'
                    inceptionYear = '2023'
                    description = project.name

                    licenses {
                        license {
                            name = 'European Union Public Licence v1.2'
                            url = 'https://www.eupl.eu/'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = "skjolber"
                            name = "Thomas Skj√∏lberg"
                            email = "thomas.rorvik.skjolberg@entur.org"
                        }
                    }
                    scm {
                        url = 'https://github.com/entur/cloud-logging'
                        connection = 'git@github.com:entur/cloud-logging.git'
                    }
                }
            }
        }

        if(project.hasProperty("signing.gnupg.keyName")) {
            signing {
                useGpgCmd()

                // set
                // signing.gnupg.keyName=xxx
                // signing.gnupg.passphrase=yyy
                // via command line or global gradle properties
                // then run
                // w17 build publishToSonatype --info --stacktrace

                sign(publishing.publications)
            }
        }

    }

}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.owasp.dependencycheck'

    // OWASP Dependency Check settings
    dependencyCheck {
        analyzedTypes = ['jar'] // the default artifact types that will be analyzed.
        format = 'ALL'
        failBuildOnCVSS = 7
        // Specifies if the build should be failed if a CVSS score equal to or above a specified level is identified.
        suppressionFiles = ["$rootDir/dependencycheck-root-suppression.xml"]
        // specify a list of known issues which contain false-positives
        scanConfigurations = configurations.findAll {
            (!it.name.startsWithAny('androidTest', 'test', 'debug', 'lint', 'kapt', 'spotbugs') && !it.name.contains("AndroidTest") && !it.name.contains("Test") && !it.name.contains("AnnotationProcessor"))
        }.collect {
            it.name
        }
    }

    eclipse {
        classpath {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    // Tell idea to output to build/classes/main instead of /out/
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true

            outputDir file('build/classes/main')
            testOutputDir file('build/classes/test')
        }
    }
    
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

gradle.projectsEvaluated {
    // do not scan testing tools and so on, see https://medium.com/@appmattus/android-security-scanning-your-app-for-known-vulnerabilities-421384603fc5
    // note: must be set after project is evaluated, as Android creates configurations based on the build types
    dependencyCheck {
        scanConfigurations = configurations.findAll {
            (!it.name.startsWithAny('androidTest', 'test', 'debug', 'lint', 'kapt') && !it.name.contains("AndroidTest") && !it.name.contains("Test") && !it.name.contains("AnnotationProcessor"))
        }.collect {
            it.name
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }

}

abstract class LogbackAlignmentRule implements ComponentMetadataRule {
    void execute(ComponentMetadataContext ctx) {
        ctx.details.with {
            if (id.group.startsWith("ch.qos.logback")) {
                belongsTo("ch.qos.logback:logback-virtual-platform:${id.version}")
            }
        }
    }
}

abstract class GrpcAlignmentRule implements ComponentMetadataRule {
    void execute(ComponentMetadataContext ctx) {
        ctx.details.with {
            if (id.group.startsWith("io.grpc")) {
                belongsTo("io.grpc:grpc-virtual-platform:${id.version}")
            }
        }
    }
}
abstract class ProtobufAlignmentRule implements ComponentMetadataRule {
    void execute(ComponentMetadataContext ctx) {
        ctx.details.with {
            if (id.group.startsWith("com.google.protobuf")) {
                belongsTo("icom.google.protobuf:protobuf-virtual-platform:${id.version}")
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
        }
    }
}




